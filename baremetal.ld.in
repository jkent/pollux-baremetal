/*
 * Copyright (C) 2013 Jeff Kent <jeff@jkent.net>
 *
 * This program is free software; you can redistribute it and/or modify
 * it under the terms of the GNU General Public License version 2 as
 * published by the Free Software Foundation.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA
 */

#include "baremetal/config.h"

OUTPUT_FORMAT("elf32-littlearm", "elf32-littlearm", "elf32-littlearm")
OUTPUT_ARCH(arm)
ENTRY(_entry)

SECTIONS
{
  . = 0x0;
  .text.early : {
    *baremetal.a:init.o(.text)
    *baremetal.a:*.o(.text)
  }
  .rodata.early : {
    *baremetal.a:init.o(.rodata)
    *baremetal.a:*.o(.rodata)
  }

  . = ALIGN(0x4000);
  main_page_table = .;
  . += (0x4000);

  .text : { *(.text) }
  .rodata : { *(.rodata) }
  .data : { *(.data) }

  . = ALIGN(4);
  __bss_start__ = .;
  .bss : { *(.bss) }
  __bss_end__ = .;

  . = ALIGN(0x1000);
  __heap_start__ = .;
  . += HEAP_SIZE;
  __heap_end__ = .;

  . = ALIGN(0x1000);
  __stack_end__ = .;
  . += COMBINED_STACK_SIZE;
  __stack_top__ = .;

  /* mthumb-interwork things */
  /DISCARD/ : { *(.glue_7 .glue_7t .vfp11_veneer .v4_bx) }
}

